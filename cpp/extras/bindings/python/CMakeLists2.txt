cmake_minimum_required(VERSION 3.21)
project(okiidoku_py
	VERSION 0.0.1
	DESCRIPTION "pybind11 build of libokiidoku"
	HOMEPAGE_URL "https://github.com/david-fong/okiidoku"
	LANGUAGES CXX
)
set(CMAKE_CXX_STANDARD 20 CACHE STRING "The C++ standard to use")
set(CMAKE_CXX_STANDARD_REQUIRED TRUE)
set(CMAKE_CXX_EXTENSIONS FALSE)

set(okiidoku_SOURCE_DIR "${PROJECT_SOURCE_DIR}/../../../")
list(APPEND CMAKE_MODULE_PATH ${okiidoku_SOURCE_DIR})

include(okiidoku/fetch_content)
include(okiidoku/compile_opts.basic)

if(PROJECT_IS_TOP_LEVEL)
	include(okiidoku/if_top_level/static_analyzers)
	include(okiidoku/if_top_level/sanitizers)
	include(CTest)
endif()

set(CMAKE_INTERPROCEDURAL_OPTIMIZATION TRUE)
# set(BUILD_SHARED_LIBS FALSE)

FetchContent_MakeAvailable(pybind11)
# https://github.com/pybind/cmake_example
# https://pybind11.readthedocs.io/en/stable/cmake/index.html

add_subdirectory(${okiidoku_SOURCE_DIR}/libs/okiidoku okiidoku)

pybind11_add_module(okiidoku_py okiidoku.pybind.cpp)
set_target_properties(okiidoku_py PROPERTIES OUTPUT_NAME "okiidoku")
target_link_libraries(okiidoku_py
	PRIVATE okiidoku_compile_options_private
	PRIVATE okiidoku
)